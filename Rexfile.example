use Rex -feature => ['1.3'];

# Telegram bots are web applications that
# respond to HTTP requests from Telegram servers.
# This is the hostname part of the URL that is also used
# to configure nginx.

set host_name => 'your-web.example.com';

# You may keep the defauls for these two.
set daemon_port => 8046;
set account => 'frfbot';

group bot_servers => get 'host_name';

my $root_dir = '/site/' . get 'host_name';
my $account = get 'account';

use Rex::Lang::Perl::Cpanm;
task "prepare", group => "bot_servers", sub {
	my $ssh_key;
	LOCAL {
		$ssh_key = cat "$ENV{HOME}/.ssh/id_rsa.pub";
	};

	create_group $account;
	account $account,
		create_home		=> 1,
		crypt_password	=> '*',
		groups			=> [$account],
		home			=> "/home/$account",
		ssh_key			=> $ssh_key;

	file [map "$root_dir/$_", qw/www conf daemon log/],
		ensure	=> 'directory',
		owner	=> $account,
		group	=> $account;

	pkg [qw/nginx libxml2-dev zlib1g-dev redis-server/],
		ensure	=> 'present';

	symlink "$root_dir/conf/nginx-site.conf", '/etc/nginx/sites-enabled/frfbot-site.conf';

	file "$root_dir/daemon/cpanfile",
		source	=> 'cpanfile',
		owner	=> $account,
		group	=> $account;

	cpanm -install();
	cpanm -installdeps => "$root_dir/daemon";

	run 'ubic-admin --batch-mode',
		unless 'test -d /etc/ubic/service';
	chmod 1777, '/var/lib/ubic';

	symlink "$root_dir/conf/ubic-service", '/etc/ubic/service/frfbot';
};

task "deploy", group => "bot_servers", sub {
	file "$root_dir/www/index.html",
		content	=> "DÃ¼nya merhaba!";

	# generate these XXX
	file "$root_dir/conf/https_priv.key",
		source	=> "conf/https_priv.key",
		mode	=> 600;
	file "$root_dir/conf/https_public_cert.pem",
		source	=> "conf/https_public_cert.pem";

	file "$root_dir/conf/frfbot.conf",
		content	=> template("conf/frfbot.conf.tmpl");
	file "$root_dir/conf/nginx-site.conf",
		content	=> template("conf/nginx-site.conf.tmpl");
	file "$root_dir/conf/ubic-service",
		content	=> template("conf/ubic-service.tmpl");

	file "$root_dir/daemon/frfbot.pl",
		source	=> "src/frfbot.pl";
	file "$root_dir/daemon/reg_hook.pl",
		source => "src/reg_hook.pl";
	file "$root_dir/daemon/Handlers.pm",
		source => "src/Handlers.pm";

	do_task "start";
};

task "start", group => "bot_servers", sub {
	run "ubic restart frfbot";

	service "redis",
		ensure	=> "started";
	service "nginx",
		ensure	=> "started";
};

task "code", group => "bot_servers", sub {
	file "$root_dir/daemon/frfbot.pl",
		source => "src/frfbot.pl";
	file "$root_dir/daemon/reg_hook.pl",
		source => "src/reg_hook.pl";
	file "$root_dir/daemon/Handlers.pm",
		source => "src/Handlers.pm";

	do_task "start";
};

auth for => 'prepare',
	user => 'root';
auth for => 'start',
	user => 'root';
auth for => 'deploy',
	user => $account;
auth for => 'code',
	user => $account;
